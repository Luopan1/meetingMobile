apply plugin: 'com.android.application'

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate
}

def getGitCommitId() {
    def cmd = 'git rev-parse --short HEAD'
    return cmd.execute().text.trim().toString()
}

def getGitBranch() {
    def cmd = 'git symbolic-ref --short -q HEAD'
    return cmd.execute().text.trim().toString()
}

def getGitLog() {
    def cmd = 'git log -1 --pretty=oneline'
    return cmd.execute().text.trim().toString()
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")
// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        applicationId "com.hezy.guide.phone"
        minSdkVersion 16
        targetSdkVersion 22
        versionName "1.0.0"
        versionCode 16777216
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //默认编译赋值
        buildConfigField "String", "GIT_BRANCH", '"' + getGitBranch() + '"'
        buildConfigField 'String', 'GIT_COMMIT', '"' + getGitCommitId() + '"'
        buildConfigField 'String', 'GIT_LOG', '"' + getGitLog() + '"'
        buildConfigField 'String', 'DATE', '"' + getDate().toString() + '"'

        buildConfigField "boolean", "IS_LOGIN", "true"  //是否直接登录
        buildConfigField 'String', 'LOGIN_TOKEN', '"9ece1ff2f67c42849b9df2991f5973da"' //直接登录的token


        jackOptions {
            enabled false
        }


        ndk {
            abiFilters "armeabi-v7a", "x86"
            // DO NOT MODIFY THIS LINE, IT'S UPDATED BY BUILD MACHINE AUTOMATICALLY.
        }
    }

    useLibrary 'org.apache.http.legacy'

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug { // debug模式
            signingConfig signingConfigs.config
            buildConfigField "boolean", "IS_DEBUG_LOG", "true"  //显示LOG
            buildConfigField "boolean", "IS_DEBUG_TOAST", "true"  //显示LOG的TOAST
            buildConfigField "boolean", "IS_HEART_LOG", "false"  //显示心跳的http日志
            buildConfigField 'String', 'API_DOMAIN_NAME', '"http://osg.apitest.haierzhongyou.com"'
            buildConfigField 'String', 'API_DOMAIN_NAME_YOYOTU', '"http://apitest.haierzhongyou.com"' //唷唷兔地址
            buildConfigField 'String', 'WS_DOMAIN_NAME', '"http://wstest.haierzhongyou.com/sales"'
            buildConfigField 'String', 'WEIXIN_APP_ID', '"wxcd220f151738cab5"' //微信id
            buildConfigField 'String', 'BUGLY_APPID', '"d2b5a75181"' //bugly
            manifestPlaceholders = [TD_APP_ID    : "02A562AEDBE243D7A64901F852E1A530",
                                    TD_CHANNEL_ID: "default"]

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
        release {
            signingConfig signingConfigs.config
            buildConfigField "boolean", "IS_DEBUG_LOG", "false"  //显示LOG
            buildConfigField "boolean", "IS_DEBUG_TOAST", "false"  //显示LOG的TOAST
            buildConfigField 'String', 'API_DOMAIN_NAME', '"http://osg.api.haierzhongyou.com"'
            buildConfigField 'String', 'API_DOMAIN_NAME_YOYOTU', '"http://api.haierzhongyou.com"' //唷唷兔地址
            buildConfigField 'String', 'WS_DOMAIN_NAME', '"http://ws.haierzhongyou.com/sales"'
            buildConfigField 'String', 'WEIXIN_APP_ID', '"wxcd220f151738cab5"' //微信id
            buildConfigField 'String', 'BUGLY_APPID', '"1ee416d265"' //bugly
            manifestPlaceholders = [TD_APP_ID    : "80F452C9B1E8427AB65DB774F7FF081F",
                                    TD_CHANNEL_ID: "default"]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                //这里修改apk文件名
                def fileName = "GuideMobile_"  + getGitBranch() + "_" + variant.buildType.name + "_" + variant.versionName + "_" + variant.versionCode + "_" + getGitCommitId() + "_" + getDate() + ".apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //底部弹窗
    compile project(':IosDialog')
    //城市选择
    compile project(path: ':citypicker')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'

    // ================agora相关================
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'org.slf4j:slf4j-api:1.7.21'
    // ================agora相关================

    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    //andbase中有
//    compile 'com.google.code.gson:gson:2.8.1'

    //图片加载
    compile 'com.squareup.picasso:picasso:2.5.2'

    // ================微信相关================
    compile 'com.tencent.mm.opensdk:wechat-sdk-android-with-mta:1.3.4'
    //崩溃检测
    compile 'com.tencent.bugly:crashreport:latest.release'
    //内存泄露检测    https://github.com/square/leakcanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    //view依赖注入根据layout 自动生成控件字段和自动查找
    compile 'com.jakewharton:butterknife:8.7.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.7.0'
    //图片带边
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    //垂直ViewPager
    compile 'me.kaelaela:verticalviewpager:1.0.0@aar'
    //照片选择
    compile 'com.jph.takephoto:takephoto_library:4.0.3'
    //rxbus
    compile 'io.reactivex:rxjava:1.2.0'
    compile 'io.reactivex:rxandroid:1.2.1'
    //socket.io
    compile('io.socket:socket.io-client:0.8.3') {
        exclude group: 'org.json', module: 'json'
    }
    //七牛云存储
    compile 'com.qiniu:qiniu-android-sdk:7.3.+'
    //fastjson
    compile 'com.alibaba:fastjson:1.2.32'


}
