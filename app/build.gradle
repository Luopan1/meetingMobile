apply plugin: 'com.android.application'

def static getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate
}

def static getGitCommitId() {
    def cmd = 'git rev-parse --short HEAD'
    return cmd.execute().text.trim().toString()
}

def static getGitBranch() {
    def cmd = 'git symbolic-ref --short -q HEAD'
    return cmd.execute().text.trim().toString()
}

def static getGitLog() {
    def cmd = 'git log -1 --pretty=oneline'
    return cmd.execute().text.trim().toString()
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")
// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        applicationId "com.hezy.guide.phone"
        minSdkVersion 16
        targetSdkVersion 27
        versionName "1.1.0"
        versionCode 16781312
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //默认编译赋值
        buildConfigField "String", "GIT_BRANCH", '"' + getGitBranch() + '"'
        buildConfigField 'String', 'GIT_COMMIT', '"' + getGitCommitId() + '"'
        buildConfigField 'String', 'GIT_LOG', '"' + getGitLog() + '"'
        buildConfigField 'String', 'DATE', '"' + getDate().toString() + '"'

        ndk {
            abiFilters "armeabi-v7a", "x86"
//            abiFilters "armeabi-v7a", "x86"
            // DO NOT MODIFY THIS LINE, IT'S UPDATED BY BUILD MACHINE AUTOMATICALLY.
        }
    }

    dataBinding {
        enabled = true
    }

    dexOptions {
        jumboMode true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug { // debug模式
            signingConfig signingConfigs.config
            buildConfigField "boolean", "IS_DEBUG_LOG", "true"  //显示LOG
            buildConfigField "boolean", "IS_DEBUG_TOAST", "true"  //显示LOG的TOAST
            buildConfigField "boolean", "IS_HEART_LOG", "false"  //显示心跳的http日志
            buildConfigField 'String', 'API_DOMAIN_NAME', '"http://osg.apitest.haierzhongyou.com"'
            buildConfigField 'String', 'API_DOMAIN_NAME_YOYOTU', '"http://api.teq6.com"' //唷唷兔地址
            buildConfigField 'String', 'WS_DOMAIN_NAME', '"http://wstest.haierzhongyou.com/sales"'
            buildConfigField 'String', 'WEIXIN_APP_ID', '"wxcd220f151738cab5"' //微信id
            buildConfigField 'String', 'BUGLY_APPID', '"d2b5a75181"' //bugly
            manifestPlaceholders = [TD_APP_ID    : "02A562AEDBE243D7A64901F852E1A530",
                                    TD_CHANNEL_ID: "default"]

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            signingConfig signingConfigs.config
            buildConfigField "boolean", "IS_DEBUG_LOG", "false"  //显示LOG
            buildConfigField "boolean", "IS_DEBUG_TOAST", "false"  //显示LOG的TOAST
            buildConfigField 'String', 'API_DOMAIN_NAME', '"http://osg.api.haierzhongyou.com"'
            buildConfigField 'String', 'API_DOMAIN_NAME_YOYOTU', '"https://api.haierzhongyou.com"' //唷唷兔地址
            buildConfigField 'String', 'WS_DOMAIN_NAME', '"http://ws.haierzhongyou.com/sales"'
            buildConfigField 'String', 'WEIXIN_APP_ID', '"wxfb5b05dd7686698b"' //微信id
            buildConfigField 'String', 'BUGLY_APPID', '"1ee416d265"' //bugly
            manifestPlaceholders = [TD_APP_ID    : "80F452C9B1E8427AB65DB774F7FF081F",
                                    TD_CHANNEL_ID: "default"]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (outputFileName.endsWith(".apk")) {
                def fileName = "Guide-Mobile_" + getGitBranch() + "_" + variant.buildType.name + "_" + variant.versionName + "_" + variant.versionCode + "_" + getDate() + ".apk"
                outputFileName = fileName
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //底部弹窗
    implementation project(':IosDialog')
    //会议拍照
    implementation project(path: ':MeetingCamera')
    //城市选择
    implementation project(path: ':citypicker')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:multidex:1.0.1'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    //图片加载
    implementation 'com.squareup.picasso:picasso:2.5.2'
    // ================微信相关================
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-with-mta:1.3.4'
    //崩溃检测
    implementation 'com.tencent.bugly:crashreport:latest.release'
    //内存泄露检测    https://github.com/square/leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    //view依赖注入根据layout 自动生成控件字段和自动查找
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //图片带边
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'com.rengwuxian.materialedittext:library:2.1.4'
    //照片选择
    implementation 'com.jph.takephoto:takephoto_library:4.0.3'
    //rxbus
    implementation 'io.reactivex:rxjava:1.3.0'
    implementation 'io.reactivex:rxandroid:1.2.1'
    // https://mvnrepository.com/artifact/io.socket/socket.io-client
    implementation('io.socket:socket.io-client:1.0.0') {
        exclude group: 'org.json', module: 'json'
    }
    implementation 'com.google.code.gson:gson:2.8.5'
    //七牛云存储
    implementation 'com.qiniu:qiniu-android-sdk:7.3.+'
    //fastjson
    implementation 'com.alibaba:fastjson:1.2.32'
    implementation 'pub.devrel:easypermissions:1.1.2'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation project(':MeetingCamera')
}
